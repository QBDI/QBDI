# Maintainer: Charles Hubain <chubain@quarkslab.com>
# Maintainer: QBDI Team <qbdi@quarkslab.com>

pkgname=QBDI-X86_64
pkgver=0.10.0
pkgrel=1
epoch=
pkgdesc="QuarkslaB Dynamic binary Instrumentation for linux-X86_64"
arch=('x86_64')
url="https://qbdi.quarkslab.com/"
license=('apache2')
options=(staticlibs)
makedepends=('cmake' 'ninja' 'gcc' 'python' 'git')

prepare() {
    mkdir "$pkgname-$pkgver"
}

build() {
    cd "$pkgname-$pkgver"
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_CROSSCOMPILING=FALSE \
          -DQBDI_PLATFORM=linux \
          -DQBDI_ARCH=X86_64 \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQBDI_TOOLS_PYQBDI=OFF \
          -DQBDI_CCACHE=OFF \
          -G Ninja \
          ../../../
    cmake --build .
}

check() {
    cd "$pkgname-$pkgver"
    ./test/QBDITest
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir/" cmake --build . --target install
}
